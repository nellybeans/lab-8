*****************************************************************************
* Naciongayo, Maria Charmane Rose
* Supan, Janelle Victoria 
*****************************************************************************

.include "8clocks.jsim"
.include "nominal.jsim"
.include "stdcell.jsim"

*****************************************************************************
* Previous add32 and fa subcircuits
*****************************************************************************

.subckt fa A B C sum carry
  x1 A B ab nand2
  x2 A ab Aab nand2 
  x3 B ab Bab nand2 
  x4 Aab Bab ABab2 nand2
  x5 C ABab2 ABab2c nand2 
  x6 ABab2 ABab2c AB2ab3c nand2 
  x7 C ABab2c ABCab2c nand2 
  x8 AB2ab3c ABCab2c sum nand2 
  x9 ab ABab2c carry nand2 
.ends 

.subckt add32 a[31:0] b[31:0] cin0 sum[31:0] cout[31:30]
  xMine a[31:0] b[31:0] cout[30:0] cin0 sum[31:0] cout[31:0] fa
.ends

*****************************************************************************
* 32-input NOR
*****************************************************************************

.subckt nor32 fas[31:0] out
    x1 fas[31:28] n01 nor4
    x2 fas[27:24] n02 nor4
    x3 fas[23:20] n03 nor4
    x4 fas[19:16] n04 nor4
    x5 fas[15:12] n05 nor4
    x6 fas[11:8] n06 nor4
    x7 fas[7:4] n07 nor4
    x8 fas[3:0] n08 nor4
    x9 n01 n02 n03 n04 n09 nand4
    x10 n05 n06 n07 n08 n10 nand4
    x11 n09 n10 out nor2
.ends

*****************************************************************************
* Join subcircuit (for connecting bus to bus)
*****************************************************************************

.subckt join a b
    .connect a b
.ends

*****************************************************************************
* New as32 subcircuit
*****************************************************************************

.subckt as32 a[31:0] b[31:0] op0 fas[31:0] n v z
    xAdd a[31:0] bout[31:0] op0 fas[31:0] c31 c30 add32
    xInvertedB b[31:0] inv[31:0] inverter
    xConnect inv[31:0] binv[31:0] join
    xOperation op0#32 b[31:0] binv[31:0] bout[31:0] mux2 
    .connect fas[31] n 
    xV c31 c30 v xor2
    xZ fas[31:0] z nor32
.ends

*****************************************************************************
* New cmp32 subcircuit
*****************************************************************************

.subckt cmp32 n v z op[2:1] fcmp[31:0]
    xInvertedN n lordN inverter
    .connect lordN notN
    xNComp v n notN finalN mux2
    xBoth z finalN finalBoth or2
    xComparator op[2]#32 op[1]#32 0#32 z#32 finalN#32 finalBoth#32 fcmp[31:0] mux4
.ends

*****************************************************************************
* Instantiation
***************************************************************************** 

xInstance a[31:0] b[31:0] op0 answer[31:0] N V Z as32
xComparator N V Z op2 op1 fempout[31:0] cmp32

*****************************************************************************
* Waveforms
* First line: Addition and subtraction test cases
* Second line: Comparator test cases (excluded the case where op[2:1] is 00)
****************************************************************************

Wa a[31:0] nrz(0, 5, 20ns, 0ns, 0.001ns, 0.001ns)
+ 0 0x55555555 0 0x55555555 -1 0 0 0xAAAAAAAA 0 0xAAAAAAAA 1 -1 0x80000000 -1
+ 2 2 2 0x80000000 0x80000000 0x80000000 3 3 3 0x7FFFFFFF 0x7FFFFFFF 0x7FFFFFFF 3 3 3 

Wb b[31:0] nrz(0, 5, 20ns, 0ns, 0.001ns, 0.001ns)
+ 0 0 0x55555555 0x55555555 -1 0 -1 0 0xAAAAAAAA 0xAAAAAAAA -1 1 0x80000000 -1
+ -3 -3 -3 2 2 2 5 5 5 -2 -2 -2 3 3 3

Wop0 op0 nrz(0, 5, 20ns, 0ns, 0.001ns, 0.001ns)
+ 0 0 0 0 0 1 1 0 0 0 0 0 0 1
+ 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1

Wop1 op1 nrz(0, 5, 20ns, 0ns, 0.001ns, 0.001ns)
+ 0 0 0 0 0 0 0 0 0 0 0 0 0 0
+ 1 1 0 1 1 0 1 1 0 1 1 0 1 1 0

Wop2 op2 nrz(0, 5, 20ns, 0ns, 0.001ns, 0.001ns)
+ 0 0 0 0 0 0 0 0 0 0 0 0 0 0
+ 0 1 1 0 1 1 0 1 1 0 1 1 0 1 1

*****************************************************************************
* Plotting
*****************************************************************************

.tran 580ns

.plot op0
.plot a[31:0]
.plot b[31:0]
.plot answer[31:0]

.plot op1
.plot op2
.plot fempout[31]